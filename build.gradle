buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.12.0'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'pl.allegro.tech.build.axion-release' version '1.9.2'
    id 'jacoco'
    id 'com.bmuschko.nexus' version '2.3.1'
}

apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag {
        prefix = project.rootProject.name
    }
    versionCreator 'versionWithBranch'

    hooks {
        pre 'fileUpdate', [files: ['README.md'], pattern: {v, p -> /'$v'/ }, replacement: {v, p -> "'$v'"}]
        pre 'commit'
    }
}

sourceCompatibility = 1.8

project.group = 'pl.allegro.tech.discovery'
project.version = scmVersion.version

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.22'

    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.7.0'

    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.6'
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'

    testCompile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.8'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.8'

    testCompile group: 'org.awaitility', name: 'awaitility', version: '2.0.0'
    testCompile group: 'org.awaitility', name: 'awaitility-groovy', version: '2.0.0'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.5.0'

    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.6'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'

    testCompile group: 'com.pszymczyk.consul', name: 'embedded-consul', version: '1.1.1'
    testCompile group: 'com.ecwid.consul', name: 'consul-api', version: '1.4.1'
}

compileJava {
    options.encoding = 'UTF-8'
    options.incremental = true
    options.compilerArgs << '-parameters'
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

wrapper {
    gradleVersion = '4.9'
}

modifyPom {
    project {
        name project.name
        description 'Java library for interacting with Consul.'
        url 'https://github.com/allegro/consul-recipes'
        inceptionYear '2018'

        scm {
            url 'https://github.com/allegro/consul-recipes'
            connection 'scm:git@github.com:allegro/consul-recipes.git'
            developerConnection 'scm:git@github.com:allegro/consul-recipes.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'skylab'
                name 'Team Skylab'
            }
        }
    }
}

nexusStaging {
    packageGroup = "pl.allegro"

    numberOfRetries = 15
    delayBetweenRetriesInMillis = 5000
}
