buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'java-library'
    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
}

scmVersion {
    tag {
        prefix = "$project.rootProject.name-"
    }
    versionCreator 'versionWithBranch'

    hooks {
        pre 'fileUpdate', [files: ['README.md'], pattern: {v, p -> /'$v'/ }, replacement: {v, p -> "'$v'"}]
        pre 'commit'
    }
}

project.group = 'pl.allegro.tech.discovery'
project.version = scmVersion.version

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.incremental = true
    options.compilerArgs << '-parameters'
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.22'
    api group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.13.1'

    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.6'
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'

    testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.1.8'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.8'

    testImplementation group: 'org.awaitility', name: 'awaitility', version: '2.0.0'
    testImplementation group: 'org.awaitility', name: 'awaitility-groovy', version: '2.0.0'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.21.0'

    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.6'
    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'

    testImplementation group: 'com.pszymczyk.consul', name: 'embedded-consul', version: '2.2.1'
    testImplementation group: 'com.ecwid.consul', name: 'consul-api', version: '1.4.1'
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

wrapper {
    gradleVersion = '7.2'
}

publishing {
    publications {
        sonatype(MavenPublication) { publication ->
            from components.java

            artifact jar
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = 'Java library for interacting with Consul.'
                url = 'https://github.com/allegro/consul-recipes'
                inceptionYear = '2018'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'skylab'
                        name = 'Team Skylab'
                    }
                }
                scm {
                    url = 'https://github.com/allegro/consul-recipes'
                    connection = 'scm:git@github.com:allegro/consul-recipes.git'
                    developerConnection = 'scm:git@github.com:allegro/consul-recipes.git'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

if (System.getenv("GPG_KEY_ID")) {
    signing {
        useInMemoryPgpKeys(
                System.getenv("GPG_KEY_ID"),
                System.getenv("GPG_PRIVATE_KEY"),
                System.getenv("GPG_PRIVATE_KEY_PASSWORD")
        )
        sign publishing.publications
    }
}
